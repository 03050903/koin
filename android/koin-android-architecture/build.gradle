apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // Koin
    compile project(":koin-android")
    // Test
    testCompile "org.koin:koin-test:$koin_version"
    testCompile "org.mockito:mockito-core:$mockito_version"
    // Support
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    // Architecture
    implementation "android.arch.lifecycle:extensions:$architectureComponents"
    annotationProcessor "android.arch.lifecycle:compiler:$architectureComponents"
}

repositories {
    jcenter()
    mavenLocal()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

group = 'org.koin'
version = "$rootProject.ext.koin_version"
description = 'KOIN - Kotlin simple dependency injection for Android Architecture Components'

ext{
    publishedGroupId = group
    libraryName = 'Koin Android Architecture'

    artifact = 'koin-android'
    libraryDescription = 'KOIN - Kotlin simple dependency injection for Android Architecture Components'
    libraryVersion = version
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId 'koin-android-architecture'

                // Add your description here
                name libraryName
                description libraryDescription

                developers {
                    developer {
                        id 'arnaudgiuliani'
                        name 'Arnaud GIULIANI'
                        email 'agiuliani@ekito.fr'
                    }
                }
                scm {
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('../local.properties').newDataInputStream())

    user = properties.getProperty('bintray_user')
    key = properties.getProperty('bintray_apikey')

    configurations = ['archives']

    publish = true //[Default: false] Whether version should be auto published after an upload
    override = true
    pkg {
        repo = 'koin'
        name = 'koin-android-architecture'
        userOrg = 'ekito'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Ekito/koin.git'
        description = libraryDescription
        version {
            desc = description
            // Optional. The passphrase for GPG signing'
            // gpg {
            //    sign = true //Determines whether to GPG sign the files. The default is false
            //    passphrase = properties.getProperty("bintray.gpg.password")
            //}
        }
    }
}