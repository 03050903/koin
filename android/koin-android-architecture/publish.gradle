apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version "$rootProject.ext.koin_version"
group "org.koin"

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/koin-android-architecture-release.aar")
            groupId
            artifactId 'koin-android-architecture'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
//    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
//    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    Properties properties = new Properties()
    properties.load(project.rootProject.file('../local.properties').newDataInputStream())
    user = properties.getProperty('bintray_user')
    key = properties.getProperty('bintray_apikey')

    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'koin'
        name = 'koin-android-architecture'
        userOrg = 'ekito'

        description = 'KOIN - Kotlin simple dependency injection for Android'
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ekito/koin.git'
        dryRun = false

        version {
            name = this.version
            desc = "koin-android-architecture ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}