group = 'org.koin'

ext {
    android_sourceSet = true
    pom_name = "koin-android"
    pom_description = "Koin project - koin-android"
}


// Dokka
apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    reportUndocumented = true
}

task dokkaJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}


// Android Publish local
apply plugin: 'digital.wup.android-maven-publish'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    duplicatesStrategy = "exclude"
    from android.sourceSets.main.java.sourceFiles
}

publishing {
    publications {
        Production(MavenPublication) {
            from components.android
            artifact sourcesJar
            artifact dokkaJar
        }
    }
}

// Bintray
apply plugin: 'com.jfrog.bintray'

bintray {
    user = bintray_user
    key = bintray_apikey
    
    pkg {
        repo = 'koin'
        name = pom_name
        userOrg = 'ekito'

        description = pom_description
        publish = true

        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ekito/koin.git'
        websiteUrl = 'https://insert-koin.io'
        dryRun = false

        publications = ['Production']
    }
}
